def chars AddOperator [$Whitespace] <+>
def chars SubtractOperator [$Whitespace] <->
def chars MultiplyOperator [$Whitespace] <*>
def chars DivideOperator [$Whitespace] </>
def chars ModuloOperator [$Whitespace] <%>
def chars InclusiveOrOperator [$Whitespace] <|>
def chars AndOperator [$Whitespace] <&>
def chars ShiftLeftOperator [$Whitespace] <\<><\<>
def chars ShiftRightOperator [$Whitespace] <\>><\>>
def chars EqualOperator [$Whitespace] <=><=>
def chars NotEqualOperator [$Whitespace] <!><=>
def chars LessThanOperator [$Whitespace] <\<>
def chars LessEqualThanOperator [$Whitespace] <\<><=>
def chars GreaterThanOperator [$Whitespace] <\>>
def chars GreaterEqualThanOperator [$Whitespace] <\>><=>
def inline chars HardCastIndicatorOperator <!>
def CastOperator ( $Type )
def HardCastOperator ( $Type $HardCastIndicatorOperator )

def inline AdditiveOperators:
  $AddOperator
  $SubtractOperator

def inline EqualityOperators:
  $EqualOperator
  $NotEqualOperator

def inline CompareOperators:
  $LessThanOperator
  $LessEqualThanOperator
  $GreaterThanOperator
  $GreaterEqualThanOperator

def inline ShiftOperators:
  $ShiftLeftOperator
  $ShiftRightOperator

def inline MultiplicativeOperators:
  $MultiplyOperator
  $DivideOperator
  $ModuloOperator

def inline CastOperators:
  $CastOperator
  $HardCastOperator

def inline PostExpression:
  ( $Expression )
  $Integer
  $Boolean
  $String
  $FunctionCall
  $Accessor

def CastExpression $CastOperators $CastExpression
def inline CastExpression $PostExpression

def merge MultiplicativeExpression $CastExpression [ $MultiplicativeOperators $MultiplicativeExpression ]
def merge AdditiveExpression $MultiplicativeExpression [ $AdditiveOperators $AdditiveExpression ]
def merge CompareExpression $AdditiveExpression [ $CompareOperators $CompareExpression ]
def merge EqualsExpression $CompareExpression [ $EqualityOperators $EqualsExpression ]
def merge ShiftExpression $EqualsExpression [ $ShiftOperators $ShiftExpression ]
def merge InclusiveOrExpression $ShiftExpression [ $InclusiveOrOperator $InclusiveOrExpression ]
def merge InclusiveAndExpression $InclusiveOrExpression [ $AndOperator $InclusiveAndExpression ]

def Expression $InclusiveAndExpression

def inline chars ArrayAccessorOpenCharacter [$Whitespace] <[>
def inline chars ArrayAccessorCloseCharacter [$Whitespace] <]>
def ArrayAccessor $ArrayAccessorOpenCharacter $Expression $ArrayAccessorCloseCharacter
def AccessorElement:
  . $Identifier
  $ArrayAccessor
def error AccessorElement Expected an Identifier </> . [$Whitespace] $AnyChar

def Accessor $Identifier { $AccessorElement }

def Boolean:
  true
  false

def inline Integer:
  [$Whitespace] $DecimalInteger

def chars DecimalInteger:
  0
  $NonZeroDecimalDigit { $DecimalDigits }
def error DecimalInteger Expected a DecimalInteger </> $NonZeroDecimalDigit [$Whitespace] $AnyChar

def chars NonZeroDecimalDigit <1-9>
def chars DecimalDigits <0-9>

def chars ArrayCharacters <[><]>

def chars AnyChar <^>
def chars AnyCharButClose <^}>
def chars AnyCharButOpen <^{>
def chars AnyCharButOpenP <^(>
def chars AnyCharButCloseP <^)>
def chars AnyCharButEquals <^=>
def chars AnyCharButSemicolon <^;>
def chars AnyCharButQuotes <^">

def chars IdentifierChars <a-zA-Z_> { <a-zA-Z0-9_> }
def chars Identifier [$Whitespace] $IdentifierChars
def chars Type [$Whitespace] { $IdentifierChars <.> } $IdentifierChars { $ArrayCharacters }
def chars clean Whitespace < \t\n> { < \t\n> }
def chars StringChars <^\n"> { <^\n"> }
def String " [$StringChars] "
def error String Expected '"' </> " [$StringChars] $AnyCharButQuotes
def chars DotWord [$Whitespace] <a-zA-Z_> { <a-zA-Z0-9_.> }

tok Using:
  using $DotWord ;
  $UsingErrors

def error UsingErrors:
  Expected a ';' </> using $DotWord [$Whitespace] $AnyChar
  Expected a namespace </> using [$Whitespace] [$AnyChar]

tok Namespace:
  namespace $DotWord ;
  $NamespaceErrors

def error NamespaceErrors:
  Expected a ';' </> namespace $DotWord [$Whitespace] $AnyChar
  Expected a namespace </> namespace [$Whitespace] [$AnyChar]

def ReturnStatement:
  return $Expression ;
  return ;
def error ReturnStatement Expected a ';' </> return

def Argument:
  $Expression

def ArgumentList:
  $Argument {, $Argument}

def FunctionCall:
  $Identifier ( [$ArgumentList] )
def error FunctionCall Expected ')' </> $Identifier ( [$ArgumentList] $AnyCharButCloseP

def Parameter $Type $Identifier
def error Parameter Expected an Identifier </> $Type [$Whitespace] [$AnyChar]

def ParameterList:
  $Parameter {, $Parameter}

def inline keyword FunctionModifier:
  native

def FunctionModifiers:
  { $FunctionModifier [$Whitespace] }

def FunctionCallStatement:
  $Identifier ( [$ArgumentList] ) ;

def AssignmentStatement:
  $Accessor = $Expression ;
def error AssignmentStatement Expected ';' </> $Accessor = $Expression $AnyChar

def CreateAssignmentStatement:
  $Type $Identifier = $Expression ;
def error CreateAssignmentStatement:
  Expected '=' </> $Type $Identifier $AnyCharButEquals
  Expected ';' </> $Type $Identifier = $Expression $AnyChar

def IfStatement:
  if ( $Expression ) $Code [ $ElseStatement ]
def error IfStatement:
  Expected '(' </> if [$Whitespace] $AnyCharButOpenP
  Expected ')' </> if ( $Expression [$Whitespace] $AnyCharButCloseP

def ElseStatement:
  else $Code

def WhileStatement:
  while ( $Expression ) $Code
def error WhileStatement:
  Expected '(' </> while [$Whitespace] $AnyCharButOpenP
  Expected ')' </> while ( $Expression [$Whitespace] $AnyCharButCloseP

def Statement:
  $ReturnStatement
  $FunctionCallStatement
  $AssignmentStatement
  $CreateAssignmentStatement
  $IfStatement
  $WhileStatement
def error Statement Not a Statement </> [$Whitespace] $AnyCharButClose

def inline chars LineCodeChars [$Whitespace] <=> <\>>

def Code:
  \{ {$Statement} \}
  $LineCodeChars $Statement
def error Code:
  Expected code </> \{
  Expected code </> $LineCodeChars

tok Function:
  $FunctionModifiers $Type $Identifier ( [$ParameterList] ) ;
  $FunctionModifiers $Type $Identifier ( [$ParameterList] ) $Code

def error FieldErrors Expected ';' </> $Type $Identifier [$Whitespace] $AnyCharButOpen

tok Field:
  $Type $Identifier ;
  $FieldErrors

def ClassField:
  $Type $Identifier ;
def error ClassField Expected ';' </> $Type $Identifier [$Whitespace] $AnyChar

def ClassMethod:
  $FunctionModifiers $Type $Identifier ( [$ParameterList] ) ;
  $FunctionModifiers $Type $Identifier ( [$ParameterList] ) $Code

def inline ClassElement:
  $ClassField
  $ClassMethod
def error ClassElement Expected a Field or a Method </> [$Whitespace] $AnyCharButClose

tok Class:
  class $Identifier \{ {$ClassElement} \}